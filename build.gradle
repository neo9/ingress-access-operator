plugins {
	id 'java'
	id 'checkstyle'
	id 'org.springframework.boot' version '3.0.5'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'org.graalvm.buildtools.native' version '0.9.20' apply false
	id "org.sonarqube" version "4.0.0.2929"
	id 'pl.allegro.tech.build.axion-release' version '1.14.4'
}

group = 'io.neo9'
version = scmVersion.version
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	maven { url 'https://repo.spring.io/release' }
	mavenCentral()
}

ext {
	javaOperatorVersion = '4.2.8'
	javaOperatorStarterVersion = '4.1.1'
	bouncycastleVersion = '1.70'

	buildNativeImage = ! "false".equalsIgnoreCase(System.getenv('NATIVE_IMAGE'))
}


checkstyleMain {
	source = sourceSets.main.java.srcDirs
}

checkstyleTest {
	source = sourceSets.test.java.srcDirs
}

// Modify checkstyle to generate an exception on checkstyle warnings.
tasks.withType(Checkstyle)*.each { checkstyleTask ->
    checkstyleTask.doLast {
        reports.all { report ->
            def outputFile = report.destination
            if (outputFile.exists() && outputFile.text.contains("<error ")) {
                throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
            }
        }
    }
}

if (buildNativeImage) {
	println "[plugins] Applying AOT plugin"
	apply plugin: "org.graalvm.buildtools.native"
} else {
	println "[plugins] NOT applying AOT plugin"
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-logging'

	implementation "io.javaoperatorsdk:operator-framework-spring-boot-starter:${javaOperatorStarterVersion}"
	implementation "org.bouncycastle:bcprov-ext-jdk15on:${bouncycastleVersion}"
	implementation "org.bouncycastle:bcpkix-jdk15on:${bouncycastleVersion}"

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor "io.javaoperatorsdk:operator-framework:${javaOperatorVersion}"
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation "io.javaoperatorsdk:operator-framework-spring-boot-starter-test:${javaOperatorStarterVersion}"
}

test {
	useJUnitPlatform()
}

bootBuildImage {
	imageName   = "neo9sas/${project.name}:latest"
	if (! buildNativeImage) {
		builder     = 'paketobuildpacks/builder:base'
	} else {
		builder		= 'paketobuildpacks/builder:tiny'
		environment = [
				'BP_NATIVE_IMAGE'                : 'true',
				'BP_NATIVE_IMAGE_BUILD_ARGUMENTS': '-H:+AddAllCharsets --enable-https'
		]
	}
}

subprojects {
    sonarqube {
        properties {
            property "sonar.sources", "src/main/java"
        }
    }
}
